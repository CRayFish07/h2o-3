import org.apache.tools.ant.taskdefs.condition.Os
import java.nio.file.FileSystems
import java.nio.file.Files
import java.nio.file.Path
import java.util.zip.GZIPOutputStream
import static java.nio.file.StandardCopyOption.*;

defaultTasks 'build_python'
description = "H2O Python Package"

dependencies {
    compile project(":h2o-assemblies:main")
}

def bv = new H2OBuildVersion(rootDir, version)

ext {
    PROJECT_VERSION = bv.getProjectVersion()
    T               = getProjectDir().toString()
    pythonexe       = findProperty("pythonExec") ?: "python"
    pipexe          = findProperty("pipExec") ?: "pip"
    if (System.env.VIRTUAL_ENV) {
        pythonexe = "${System.env.VIRTUAL_ENV}/bin/python".toString()
        pipexe = "${System.env.VIRTUAL_ENV}/bin/pip".toString()
    }
    testsPath        = file("tests")
}

task setProjectVersion << {
    File INIT = new File([T, "h2o", "__init__.py"].join(File.separator))
    println "    INIT.path = " + INIT.path
    def txt = INIT.text
    txt = txt.replaceAll("SUBST_PROJECT_VERSION", PROJECT_VERSION)
            .replaceAll("SUBST_PROJECT_BUILDINFO", bv.toString())

    INIT.write(txt)
}

task resetProjectVersion << {
    File INIT = new File([T, "h2o", "__init__.py"].join(File.separator))
    println "    INIT.path = " + INIT.path
    def txt = INIT.text
    txt = txt.replaceAll(PROJECT_VERSION, "SUBST_PROJECT_VERSION")
            .replaceAll("__buildinfo__ = .*", "__buildinfo__ = \"SUBST_PROJECT_BUILDINFO\"")
    INIT.write(txt)
}

task verifyDependencies(type: Exec) {
    commandLine getOsSpecificCommandLine([
            pythonexe, "scripts/verify_requirements.py", "--metayaml", "conda/h2o/meta.yaml"
    ])
}

task buildDist(type: Exec) {
    dependsOn verifyDependencies
    dependsOn setProjectVersion

    doFirst {
        file("${buildDir}/tmp").mkdirs()
        standardOutput = new FileOutputStream(file("${buildDir}/tmp/h2o-py_buildDist.out"))
    }
    commandLine getOsSpecificCommandLine([pythonexe, "setup.py", "bdist_wheel"])
}

task smokeTest(type: Exec) {
    dependsOn build
    println "PyUnit smoke test (run.py --wipeall --testsize s)..."
    workingDir testsPath

	List<java.lang.String> args = [pythonexe, '../../scripts/run.py', '--wipeall', '--testsize', 's']
    if (project.hasProperty("jacocoCoverage")) {
        args << '--jacoco'
    }
    commandLine getOsSpecificCommandLine(args)
}

task pythonVersion(type: Exec) {
    doFirst {
        println(environment)
    }
    commandLine getOsSpecificCommandLine([pythonexe, "--version"])
}

task cleanUpSmokeTest(type: Delete) {
    delete file("tests/results")
}

task cleanCoverageData(type: Delete) {
    delete files("${testsPath}/results/jacoco")
}

task cleaner(type: Delete) {
    doFirst {
        println "Cleaning..."
    }
    delete file("dist/")
    delete file("h2o.egg-info/")
    delete file("build/")
    delete fileTree(dir: "$projectDir/h2o" , include: '**/*.pyc')
}

clean.dependsOn cleaner, cleanUpSmokeTest
resetProjectVersion.dependsOn buildDist
task build_python(dependsOn: resetProjectVersion)
build.dependsOn build_python
